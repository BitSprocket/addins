VERSION 5.00
Object = "{831FDD16-0C5C-11D2-A9FC-0000F8754DA1}#2.0#0"; "MSCOMCTL.OCX"
Begin VB.UserDocument ToolCodeView 
   ClientHeight    =   3750
   ClientLeft      =   0
   ClientTop       =   0
   ClientWidth     =   4305
   HScrollSmallChange=   225
   ScaleHeight     =   3750
   ScaleWidth      =   4305
   VScrollSmallChange=   225
   Begin VB.ListBox lstDbg 
      Height          =   840
      Left            =   2250
      TabIndex        =   4
      Top             =   990
      Visible         =   0   'False
      Width           =   1680
   End
   Begin VB.Frame fraFilter 
      BorderStyle     =   0  'None
      Height          =   375
      Left            =   45
      TabIndex        =   1
      Top             =   2070
      Width           =   2085
      Begin VB.TextBox txtFilter 
         Height          =   285
         Left            =   450
         TabIndex        =   2
         Top             =   0
         Width           =   1500
      End
      Begin VB.Label lblFilter 
         Caption         =   "Filter:"
         Height          =   240
         Left            =   0
         TabIndex        =   3
         Top             =   45
         Width           =   420
      End
   End
   Begin VB.Timer Timer 
      Enabled         =   0   'False
      Interval        =   5000
      Left            =   840
      Top             =   1440
   End
   Begin MSComctlLib.ImageList Babel 
      Left            =   180
      Top             =   1320
      _ExtentX        =   1005
      _ExtentY        =   1005
      BackColor       =   -2147483643
      ImageWidth      =   16
      ImageHeight     =   16
      MaskColor       =   12632256
      _Version        =   393216
      BeginProperty Images {2C247F25-8591-11D1-B16A-00C0F0283628} 
         NumListImages   =   16
         BeginProperty ListImage1 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":0000
            Key             =   ""
         EndProperty
         BeginProperty ListImage2 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":039A
            Key             =   ""
         EndProperty
         BeginProperty ListImage3 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":0734
            Key             =   ""
         EndProperty
         BeginProperty ListImage4 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":0ACE
            Key             =   ""
         EndProperty
         BeginProperty ListImage5 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":0E68
            Key             =   ""
         EndProperty
         BeginProperty ListImage6 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":1202
            Key             =   ""
         EndProperty
         BeginProperty ListImage7 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":159C
            Key             =   ""
         EndProperty
         BeginProperty ListImage8 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":1936
            Key             =   ""
         EndProperty
         BeginProperty ListImage9 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":1CD0
            Key             =   ""
         EndProperty
         BeginProperty ListImage10 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":206A
            Key             =   ""
         EndProperty
         BeginProperty ListImage11 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":2404
            Key             =   ""
         EndProperty
         BeginProperty ListImage12 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":299E
            Key             =   ""
         EndProperty
         BeginProperty ListImage13 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":2D38
            Key             =   ""
         EndProperty
         BeginProperty ListImage14 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":30D2
            Key             =   ""
         EndProperty
         BeginProperty ListImage15 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":366C
            Key             =   ""
         EndProperty
         BeginProperty ListImage16 {2C247F27-8591-11D1-B16A-00C0F0283628} 
            Picture         =   "ToolCodeView.dox":3A06
            Key             =   ""
         EndProperty
      EndProperty
   End
   Begin MSComctlLib.TreeView CodeView 
      Height          =   2000
      Left            =   0
      TabIndex        =   0
      Top             =   0
      Width           =   2000
      _ExtentX        =   3519
      _ExtentY        =   3519
      _Version        =   393217
      HideSelection   =   0   'False
      Indentation     =   0
      LabelEdit       =   1
      LineStyle       =   1
      Sorted          =   -1  'True
      Style           =   7
      ImageList       =   "Babel"
      Appearance      =   1
   End
End
Attribute VB_Name = "ToolCodeView"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
Option Explicit

'todo: save expanded states from last run?
'      if filter active auto expand parent nodes if has results?
'      right click extract function?

Public ActiveCodeModule As CodeModule
Public CurMod As CodeModule
Public ProjectName As String

Private filterChanged As Boolean
Private PrevMemberCount As Long
Private Line As Long
Private Nodes(5) As Node
Private initilized As Boolean

Const DEBUGMODE = False

Public Sub DBG(msg As String)
    If DEBUGMODE Then lstDbg.AddItem msg & " " & Format(Now, "h:m:ss")
End Sub

Private Sub CodeView_DblClick()

    If ActiveCodeModule Is Nothing Then Exit Sub
    
    If Not CodeView.SelectedItem Is Nothing Then
        ActiveCodeModule.parent.Activate
        ActiveCodeModule.CodePane.SetSelection Line, 1, Line, 1
        ActiveCodeModule.CodePane.Window.SetFocus
    End If
    
End Sub

Private Sub CodeView_KeyDown(KeyCode As Integer, Shift As Integer)
    If KeyCode = 116 Then  'f5
        filterChanged = True
        Reload
    End If
End Sub

Private Sub CodeView_NodeClick(ByVal Node As MSComctlLib.Node)
    Dim value As String

    If ActiveCodeModule Is Nothing Then Exit Sub
    
    value = Node.key
    
    If Len(value) < 4 Then Exit Sub
    
    Line = Mid(value, 4, Len(value) - 4)
    ActiveCodeModule.CodePane.TopLine = Line
    Line = Line + 1
    
End Sub


'this has to be called from reload and not a form_show
'on startup reload was being called first, then UserDocument_Show was calling InitCodeView which was blanking out
'the initial load leading to a confusing non-display _sometimes_
Sub InitCodeView()
    On Error GoTo hell
    
    Const hash As String = "#"
    Dim n As Node

    Line = 1
    initilized = True
    CodeView.Nodes.Clear
    Set n = CodeView.Nodes.Add(, , "cv", "CodeView", 16)
    
    n.Bold = True
    n.Expanded = True
    
    Set Nodes(vbext_mt_Const) = CodeView.Nodes.Add("cv", tvwChild, vbext_mt_Const & hash, "Constants", vbext_mt_Const)
    Set Nodes(vbext_mt_Variable) = CodeView.Nodes.Add("cv", tvwChild, vbext_mt_Variable & hash, "Variables", vbext_mt_Variable)
    Set Nodes(vbext_mt_Property) = CodeView.Nodes.Add("cv", tvwChild, vbext_mt_Property & hash, "Properties", vbext_mt_Property)
    Set Nodes(vbext_mt_Event) = CodeView.Nodes.Add("cv", tvwChild, vbext_mt_Event & hash, "Events", vbext_mt_Event)
    Set Nodes(vbext_mt_Method) = CodeView.Nodes.Add("cv", tvwChild, vbext_mt_Method & hash, "Functions", vbext_mt_Method)
    
    Nodes(vbext_mt_Event).Expanded = False 'to many
    Nodes(vbext_mt_Method).Expanded = True
    
    Exit Sub
hell:
    MsgBox "Err in InitCodeView: " & Err.Description
End Sub

Sub ClearCodeView()
    On Error Resume Next
    Freeze CodeView.hwnd
    ClearChildNodes CodeView, vbNullString, Nodes(vbext_mt_Const)
    ClearChildNodes CodeView, vbNullString, Nodes(vbext_mt_Variable)
    ClearChildNodes CodeView, vbNullString, Nodes(vbext_mt_Event)
    ClearChildNodes CodeView, vbNullString, Nodes(vbext_mt_Property)
    ClearChildNodes CodeView, vbNullString, Nodes(vbext_mt_Method)
    Unfreeze CodeView.hwnd
End Sub

Sub Reload(Optional NewModule As CodeModule)

    Const hash As String = "#"
    Dim member As Object
    Dim n As Node
    Dim I As Long
    Dim pos As Long
    Dim loc As Long
    Dim mType As Long
    Dim curProj As String
    
    On Error Resume Next
    
    If Not initilized Then InitCodeView
    
    curProj = g_VBInstance.ActiveVBProject.Name
    If curProj <> ProjectName Then
        If Len(ProjectName) > 0 Then 'project name has changed..
            SaveMySetting ProjectName, Trim(txtFilter.text)
            DBG "Saving filter for " & ProjectName
        End If
        ProjectName = curProj
        txtFilter.text = Trim(GetMySetting(ProjectName, ""))
        DBG "Loading filter for " & ProjectName
    End If
    
    If Not NewModule Is Nothing Then
        If CurMod Is Nothing And ActiveCodeModule Is Nothing Then
             Set ActiveCodeModule = NewModule
        ElseIf ObjPtr(CurMod) <> ObjPtr(ActiveCodeModule) Then
            Set ActiveCodeModule = NewModule
            DBG "New Module"
        End If
    End If
   
    If ActiveCodeModule Is Nothing Then
        DBG "ActiveMod nothing"
        Exit Sub
    End If
    
    If ObjPtr(CurMod) = ObjPtr(ActiveCodeModule) And Not filterChanged Then
        DBG "No change"
        Exit Sub
    End If
    
    DBG "Loading: " & ActiveCodeModule.parent.Name
    
    If ActiveCodeModule.Members.Count = 0 Then
        DBG "Members.count = 0"
        Exit Sub
    End If
    
    Set CurMod = ActiveCodeModule
    filterChanged = False
    ClearCodeView
    
    Freeze CodeView.hwnd
    For Each member In ActiveCodeModule.Members
        I = 0
        
        If member.Scope = vbext_Private Then
            I = 5
        ElseIf member.Scope = vbext_Friend Then
            I = 10
        End If
        
        mType = member.Type
        If mType = vbext_mt_Method Then
            pos = InStr(member.Name, "_")
            If pos > 3 Then
                mType = vbext_mt_Event
            End If
        End If
        
        loc = member.CodeLocation
        
        Select Case mType
            Case vbext_mt_Method, vbext_mt_Event
                loc = ActiveCodeModule.ProcBodyLine(member.Name, vbext_pk_Proc)
            Case vbext_mt_Property
                loc = ActiveCodeModule.ProcBodyLine(member.Name, vbext_pk_Get)
                If loc = 0 Then loc = ActiveCodeModule.ProcBodyLine(member.Name, vbext_pk_Let)
                If loc = 0 Then loc = ActiveCodeModule.ProcBodyLine(member.Name, vbext_pk_Set)
        End Select
        
        If Len(txtFilter) > 0 Then
            If InStr(1, member.Name, txtFilter, vbTextCompare) > 0 Then
                CodeView.Nodes.Add Nodes(mType), tvwChild, member.Scope & mType & hash & loc & hash, member.Name, mType + I
            End If
        Else
            CodeView.Nodes.Add Nodes(mType), tvwChild, member.Scope & mType & hash & loc & hash, member.Name, mType + I
        End If
                
    Next
    
    CodeView.Nodes("cv").text = ActiveCodeModule.parent.Name
    Nodes(vbext_mt_Const).Sorted = True
    Nodes(vbext_mt_Variable).Sorted = True
    Nodes(vbext_mt_Property).Sorted = True
    Nodes(vbext_mt_Method).Sorted = True
    Nodes(vbext_mt_Event).Sorted = True
    Unfreeze CodeView.hwnd
    
End Sub

Private Sub lblFilter_Click()
    txtFilter.text = ""
End Sub

Private Sub Timer_Timer()

    
'    'this used to autoreload if line count changed..this is to often
'    'the following might work better refreshing the treeview when a complete new member was added..
'    If Not ActiveCodeModule Is Nothing Then
'        If ActiveCodeModule.Members.Count <> PrevMemberCount Then
'            PrevMemberCount = ActiveCodeModule.Members.Count
'            filterChanged = True
'        End If
'    End If
'
'    'reloading constantly while coding has side effects such as causes jumps in syntax check etc
'    'reload function will exit with no changes if module has not changed, and if filter has not changed
'    Reload
    
End Sub

Private Sub txtFilter_Change()
    filterChanged = True
    Reload
End Sub

Private Sub UserDocument_Resize()
    On Error Resume Next
    
    CodeView.Width = UserDocument.ScaleWidth - 50
    CodeView.Height = UserDocument.ScaleHeight - fraFilter.Height - 50
    fraFilter.Top = UserDocument.ScaleHeight - fraFilter.Height - 25
    fraFilter.Width = CodeView.Width
    txtFilter.Width = fraFilter.Width - txtFilter.Left - 25
    
    If DEBUGMODE Then
        CodeView.Height = (CodeView.Height - 50) / 2
        lstDbg.Height = CodeView.Height
        lstDbg.Top = CodeView.Top + CodeView.Height + 25
        lstDbg.Width = CodeView.Width
    End If
    
End Sub

Private Sub UserDocument_Show()
    On Error Resume Next
    lstDbg.Visible = DEBUGMODE
    lstDbg.Left = CodeView.Left
    UserDocument.MinWidth = 100  'we dont want scroll bars to auto show..
    UserDocument.MinHeight = 100
    If Not initilized Then InitCodeView
    If ActiveCodeModule Is Nothing Then Reload g_VBInstance.ActiveCodePane.CodeModule
End Sub

Private Sub UserDocument_Terminate()
    On Error Resume Next
    SaveMySetting ProjectName, Trim(txtFilter.text)
End Sub
